# terraform init && terraform apply -var="region=eu-west-3" -var="bucket_name=[...]"

terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.0"
    }
  }
}

variable "region" {
  type        = string
  description = "The AWS region where the resources should be created."
}

variable "bucket_name" {
  type        = string
  description = "The bucket name where all of the challenges should be stored."
}

variable "cors_allowed_origins" {
  type        = list(string)
  description = "Allowed origins for bucket CORS."
  default     = ["*"]
}

provider "aws" {
  region = var.region
}

resource "aws_s3_bucket" "challenges" {
  bucket        = var.bucket_name
  force_destroy = true
}

resource "aws_s3_bucket_cors_configuration" "challenges" {
  bucket = aws_s3_bucket.challenges.id

  cors_rule {
    allowed_methods = ["GET", "HEAD"]
    allowed_origins = var.cors_allowed_origins
    allowed_headers = ["*"]
    max_age_seconds = 3600
  }
}

resource "aws_s3_bucket_ownership_controls" "ownership" {
  bucket = aws_s3_bucket.challenges.id

  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}

resource "aws_s3_bucket_acl" "public_read" {
  bucket = aws_s3_bucket.challenges.id
  acl    = "private"

  depends_on = [
    aws_s3_bucket_ownership_controls.ownership,
  ]
}

resource "aws_iam_user" "rctf" {
  name          = "rctf-bucket"
  force_destroy = true
}

resource "aws_iam_access_key" "rctf" {
  user = aws_iam_user.rctf.name
}

data "aws_iam_policy_document" "rctf_bucket_access" {
  statement {
    sid     = "ListBucket"
    effect  = "Allow"
    actions = ["s3:ListBucket"]
    resources = [
      aws_s3_bucket.challenges.arn,
    ]
  }

  statement {
    sid    = "ObjectCrud"
    effect = "Allow"
    actions = [
      "s3:GetObject",
      "s3:PutObject",
      "s3:DeleteObject",
      "s3:GetObjectAcl",
      "s3:PutObjectAcl",
    ]
    resources = [
      "${aws_s3_bucket.challenges.arn}/*",
    ]
  }
}

resource "aws_iam_user_policy" "rctf_bucket_access" {
  name   = "rctf-bucket-access"
  user   = aws_iam_user.rctf.name
  policy = data.aws_iam_policy_document.rctf_bucket_access.json
}

output "access_key_id" {
  value     = aws_iam_access_key.rctf.id
  sensitive = true
}

output "secret_access_key" {
  value     = aws_iam_access_key.rctf.secret
  sensitive = true
}

output "rctf_iam_user_arn" {
  value = aws_iam_user.rctf.arn
}

output "bucket" {
  value = aws_s3_bucket.challenges.bucket_regional_domain_name
}